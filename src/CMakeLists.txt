include(ExternalProject)

add_definitions(
  -std=c++14
 # -fno-rtti
  )

# Helper function
function(prepend list prefix)
   set(tmpvar "")
   foreach(x ${ARGN})
      list(APPEND tmpvar "${prefix}${x}")
   endforeach(x)
   set(${list} "${tmpvar}" PARENT_SCOPE)
endfunction(prepend)

# Download and build maphoon
ExternalProject_Add(
  maphoon2008c
  URL http://www.ii.uni.wroc.pl/~nivelle/teaching/compilers2015/maphoon2008c.tar.gz
  URL_HASH SHA256=edeae39bda17da65975e6225fabf91b6c2fd2f98089f2d83f02e135e717c67b0
  # Maphoon comes pre-compiled, and does not support out-of-source builds.
  BUILD_IN_SOURCE 1
  CONFIGURE_COMMAND true
  BUILD_COMMAND make
  INSTALL_COMMAND true
  )
# Determine the maphoon binary path
ExternalProject_Get_Property(maphoon2008c SOURCE_DIR)
set(MAPHOON_BIN ${SOURCE_DIR}/maphoon)

# Prepare for using maphoon
set(MAPHOON_WORKDIR ${CMAKE_CURRENT_BINARY_DIR})
set(MAPHOON_IDEEX ${SOURCE_DIR}/idee.x)
set(MAPHOONGEN token.cpp parser.cpp)
prepend(MAPHOONGEN ${MAPHOON_WORKDIR}/ ${MAPHOONGEN})

set(MAPHOON_INPUT ${CMAKE_CURRENT_SOURCE_DIR}/parsing/grammar.m)

# Use maphoon to generate the parser
add_custom_command(
  OUTPUT ${MAPHOONGEN}
  WORKING_DIRECTORY ${MAPHOON_WORKDIR}
  DEPENDS maphoon2008c
  DEPENDS ${MAPHOON_INPUT}
  COMMAND cp ${MAPHOON_IDEEX} .
  COMMAND ${MAPHOON_BIN} ${MAPHOON_INPUT}
  COMMENT "Generating parser with Maphoon"
  )

FLEX_TARGET(lexer parsing/lexer.lex ${CMAKE_CURRENT_BINARY_DIR}/lexer.cpp)

file(GLOB_RECURSE SOURCES
  ./*.cpp
  )

# LLVM detection package for cmake is very limited :(
link_directories(${LLVM_LIBRARY_DIR})
set(LLVM_LIBRARIES LLVMCore LLVMScalarOpts)

# Adding sources dir to include list, so that files generated by maphoon can see our headers and vice versa
include_directories(
  ${CMAKE_CURRENT_SOURCE_DIR}
  ${CMAKE_CURRENT_SOURCE_DIR}/parsing
  ${CMAKE_CURRENT_SOURCE_DIR}/ast
  ${CMAKE_CURRENT_SOURCE_DIR}/misc
  ${CMAKE_CURRENT_SOURCE_DIR}types
  ${CMAKE_CURRENT_SOURCE_DIR}/world
  ${CMAKE_CURRENT_BINARY_DIR}
  ${LLVM_INCLUDE_DIRS}
  )

# The executable is created from *.cpp sources and the file prepared by flex
add_executable(
  ${CMAKE_PROJECT_NAME}   # Executable name
  ${SOURCES}              # Our sources
  ${FLEX_lexer_OUTPUTS}   # Files generated by flex
  ${MAPHOONGEN}           # Files generated by maphoon
  )

# Generate grammar preview
set(GRAMMAR_PREVIEW ${CMAKE_CURRENT_BINARY_DIR}/grammar.txt)
add_custom_command(
  OUTPUT ${GRAMMAR_PREVIEW}
  DEPENDS ${MAPHOON_INPUT}
  COMMAND grep '%' ${MAPHOON_INPUT} > ${GRAMMAR_PREVIEW}
  COMMENT "Generating grammar preview in ${GRAMMAR_PREVIEW}"
  )
add_custom_target(
  grammar_preview ALL
  DEPENDS ${GRAMMAR_PREVIEW}
  )

set_property(
  TARGET ${CMAKE_PROJECT_NAME}
  APPEND PROPERTY OBJECT_DEPENDS ${GRAMMAR_PREVIEW}
  )

# Link target app with flex
target_link_libraries(
  ${CMAKE_PROJECT_NAME}
  fl                      # Flex
  ${LLVM_LIBRARIES}
  )

# Copy the final binary to outside src dir
add_custom_command(
  TARGET ${CMAKE_PROJECT_NAME} POST_BUILD
  COMMAND ${CMAKE_COMMAND} -E copy "${CMAKE_CURRENT_BINARY_DIR}/${CMAKE_CFG_INTDIR}/${CMAKE_PROJECT_NAME}${CMAKE_EXECUTABLE_SUFFIX}" "${CMAKE_BINARY_DIR}/"
)
