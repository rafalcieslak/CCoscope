include(ExternalProject)

# Helper function

function(prepend list prefix)
   set(tmpvar "")
   foreach(x ${ARGN})
      list(APPEND tmpvar "${prefix}${x}")
   endforeach(x)
   set(${list} "${tmpvar}" PARENT_SCOPE)
endfunction(prepend)

# Download and build maphoon
ExternalProject_Add(
  maphoon2008c
  URL http://www.ii.uni.wroc.pl/~nivelle/teaching/compilers2015/maphoon2008c.tar.gz
  URL_HASH SHA256=edeae39bda17da65975e6225fabf91b6c2fd2f98089f2d83f02e135e717c67b0
  # Maphoon comes pre-compiled, and does not support out-of-source builds.
  BUILD_IN_SOURCE 1
  CONFIGURE_COMMAND true
  BUILD_COMMAND make
  INSTALL_COMMAND true
  )
# Determine the maphoon binary path
ExternalProject_Get_Property(maphoon2008c SOURCE_DIR)
set(MAPHOON_BIN ${SOURCE_DIR}/maphoon)

# Prepare for using maphoon
set(MAPHOON_WORKDIR ${CMAKE_CURRENT_BINARY_DIR})
set(MAPHOON_IDEEX ${SOURCE_DIR}/idee.x)
set(MAPHOONGEN token.cpp parser.cpp)
prepend(MAPHOONGEN ${MAPHOON_WORKDIR}/ ${MAPHOONGEN})

set(MAPHOON_INPUT ${CMAKE_CURRENT_SOURCE_DIR}/grammar.m)

add_custom_command(
  OUTPUT ${MAPHOONGEN}
  WORKING_DIRECTORY ${MAPHOON_WORKDIR}
  DEPENDS maphoon2008c
  DEPENDS ${MAPHOON_INPUT}
  COMMAND cp ${MAPHOON_IDEEX} .
  COMMAND ${MAPHOON_BIN} ${MAPHOON_INPUT}
  )

FLEX_TARGET(tokens tokens.lex ${CMAKE_CURRENT_BINARY_DIR}/tokens.cpp)

file(GLOB SOURCES
  ./*.cpp
  )

# Adding sources dir to include list, so that files generated by maphoon can see our headers
include_directories(${CMAKE_CURRENT_SOURCE_DIR})
# And vice-versa
include_directories(${CMAKE_CURRENT_BINARY_DIR})

# The executable is created from *.cpp sources and the file prepared by flex
add_executable(
  ${CMAKE_PROJECT_NAME}   # Executable name
  ${SOURCES}              # Our sources
  ${FLEX_tokens_OUTPUTS}  # Files generated by flex
  ${MAPHOONGEN}           # Files generated by maphoon
  )

# Link target app with flex
target_link_libraries(
  ${CMAKE_PROJECT_NAME}
  fl
  )

# Copy the final binary to outside src dir
add_custom_command(
  TARGET ${CMAKE_PROJECT_NAME} POST_BUILD
  COMMAND ${CMAKE_COMMAND} -E copy "${CMAKE_CURRENT_BINARY_DIR}/${CMAKE_CFG_INTDIR}/${CMAKE_PROJECT_NAME}${CMAKE_EXECUTABLE_SUFFIX}" "${CMAKE_BINARY_DIR}/"
  )
